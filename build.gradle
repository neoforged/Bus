import net.neoforged.gradleutils.PomUtilsExtension

buildscript {
    dependencies {
        classpath('de.jjohannes.gradle:extra-java-module-info:0.14')
    }
}
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradleutils' version '4.1.0'
    id 'net.neoforged.licenser' version '0.7.2' apply false
    id 'net.neoforged.jarcompatibilitychecker' version '0.1.13'
}

gradleutils {
    setupCentralPublishing()
    setupSigning(signAllPublications: true)
}

checkJarCompatibility {
    isAPI = true
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'de.jjohannes.extra-java-module-info'
    apply plugin: 'net.neoforged.licenser'

    group 'net.neoforged'
    java {
        toolchain.languageVersion = JavaLanguageVersion.of(17)
        modularity.inferModulePath.set(true)
    }
    gradleutils {
        version {
            branches.suffixBranch.set(true)
        }
    }
    version = gradleutils.version
    
    repositories {
        mavenLocal()
        maven {
            name = 'forge'
            url = 'https://maven.neoforged.net/'
        }
    }

    extraJavaModuleInfo {
        failOnMissingModuleInfo = false
        automaticModule('jmh-core-1.35.jar', 'jmh.core')
        automaticModule('jopt-simple-5.0.4.jar', 'jopt.simple') // TODO: Update ModLauncher to use jopt 6.0-alpha-3+, which sets the module name to 'joptsimple'
    }
    
    // Hack eclipse into knowing that the gradle deps are modules
    eclipse {
        classpath {
            containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
            file {
                whenMerged {
                    entries.findAll { it.kind == 'con' && it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each {
                        it.entryAttributes['module'] = 'true'
                    }
                }
            }
        }
    }

    license {
        header = rootProject.file("codeformat/HEADER.txt")
        skipExistingHeaders = true
        include '**/*.java'
    }

    javadoc {
        options.addBooleanOption('Xdoclint:none', true)
    }
}

changelog {
    from '7.0'
}

logger.lifecycle('Version: ' + version)

ext.sharedManifest = manifest {
    attributes([
        'Specification-Title': 'Bus',
        'Specification-Vendor': 'NeoForged',
        'Specification-Version': '1', // Currently version 1 of the eventbus specification
        'Implementation-Title': project.name,
        'Implementation-Version': "${project.version}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
        'Implementation-Vendor' :'NeoForged',
        'Git-Commit': gradleutils.gitInfo.abbreviatedId,
        'Git-Branch': gradleutils.gitInfo.branch,
    ] as LinkedHashMap, 'net/neoforged/bus/service/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}
java.withSourcesJar()
java.withJavadocJar()

dependencies {
    implementation('org.ow2.asm:asm:9.2')
    implementation('org.apache.logging.log4j:log4j-api:2.17.1')
    compileOnly('org.jetbrains:annotations:23.0.0')
    api('net.jodah:typetools:0.6.3')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Bus'
                description = 'High performance Event Bus library'
                pomUtils.githubRepo(it, 'Bus')
                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, PomUtilsExtension.License.LGPL_v2)
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}
